plugins {
    id 'org.springframework.boot' version '2.1.3.RELEASE'
    id 'java'
}

apply plugin: 'io.spring.dependency-management'
group 'com.zp'
version '1.0-SNAPSHOT'

sourceCompatibility = '1.8'

buildDir = 'out'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

task copyConfigResources(type: Copy) {
    from "${projectDir}/src/main/resources"
    into "${buildDir}/classes/java/main"
}
processResources.dependsOn copyConfigResources

repositories {
    maven {
        url 'http://maven.aliyun.com/nexus/content/groups/public/'
    }
    mavenCentral()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'lib')

    implementation 'org.springframework.boot:spring-boot-starter'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    implementation 'com.microsoft.sqlserver:mssql-jdbc'

    // mysql
    implementation 'mysql:mysql-connector-java'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // fastjson
    implementation 'com.alibaba:fastjson:1.2.44'

    implementation 'org.apache.httpcomponents:httpclient'

}
// 清除现有的lib目录
task clearJar(type: Delete) {
    delete "$buildDir\\libs\\lib"
}

// 将依赖包复制到lib目录
task copyJar(type: Copy, dependsOn: 'clearJar') {
    from configurations.compileClasspath
    into "$buildDir\\libs\\lib"
}

task clearConfigs(type: Copy) {
    delete "${buildDir}\\libs\\configs"
}

task copyConfigs(type: Copy, dependsOn: 'clearConfigs') {
    from "${projectDir}/src/main/resources"
    into "${buildDir}\\libs\\configs"
}

bootJar {
    // 例外所有的jar
    excludes = ["*.jar"]
    // lib目录的清除和复制任务
    dependsOn clearJar
    dependsOn copyJar
    dependsOn clearConfigs
    dependsOn copyConfigs
    
    // 指定依赖包的路径
    manifest {
        attributes "Manifest-Version": 1.0,
                'Class-Path': configurations.compileClasspath.files.collect { "lib/$it.name" }.join(' ')
    }
}